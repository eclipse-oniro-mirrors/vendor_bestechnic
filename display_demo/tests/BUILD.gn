# Copyright (c) 2021 bestechnic (Shanghai) Technologies CO., LIMITED.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

declare_args() {
  bes_fs_enable = true
  bes_fs_test = false
  bes_histreamer_test = false
  bes_ui_enable = true
  bes_ui_test = false
  bes_ability_enable = false
  bes_ace_enable = true
  bes_ability_test = false
}

static_library("example") {
  sources = [
    "app.cpp",
    "example.c",
  ]
  defines = []
  deps = [ "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_static" ]

  include_dirs = [
    "//utils/native/lite/include",
    "//third_party/musl/porting/liteos_m/kernel/include",
    "${device_path}/components/ui",
  ]

  if (bes_histreamer_test) {
    deps += [ "histreamer:histreamer_test" ]
    defines += [ "HISTREAMER_TEST" ]
  }

  if (bes_fs_enable && bes_fs_test) {
    deps += [ "fs:fs_test" ]
    defines += [ "FS_TEST" ]
    include_dirs += [ "fs" ]
  } else if (bes_ui_enable && bes_ui_test) {
    deps += [ "ui:ui_test" ]
    defines += [ "UI_TEST" ]
    include_dirs += [ "ui" ]
  } else if (bes_ability_enable && bes_ace_enable && bes_ability_test) {
    deps += [ "ability:ability_test" ]
    defines += [ "ABILITY_TEST" ]
    include_dirs += [ "ability" ]
  }
  deps += [ "wifi:wifi_test" ]
}
